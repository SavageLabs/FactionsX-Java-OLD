plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'io.illyria'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    // Spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    // PlaceholderAPI
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // JSON Message
    maven { url 'https://rayzr.dev/repo/' }
    // Vault, Dynmap
    maven { url 'https://jitpack.io' }
    // Essentials
    maven { url 'https://ci.ender.zone/plugin/repository/everything/' }
    // PaperLib
    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'me.clip:placeholderapi:2.10.4'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'net.ess3:EssentialsX:2.17.1'
    compileOnly 'com.github.webbukkit.dynmap:dynmap-api:v3.0-beta-4'
    compileOnly name: 'CMIAPI7.6.2.0'
    compileOnly name: 'SuperVanish'
    compileOnly('org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
        exclude group: 'javax.persistence', module: 'persistence-api'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'com.google.code.gson:gson:2.8.5'
    compile 'me.rayzr522:jsonmessage:1.2.0'
    compile "io.papermc:paperlib:1.0.2"
}

processResources {
    eachFile { details ->
        if (details.name.endsWith('.yml')) { // or whatever pattern/criteria is appropriate
            filter {
                String line -> line.replaceAll('\\$\\{project.version}', version.toString())
            }
        }
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    relocate 'com.google', 'io.illyria.shade.google'
    relocate 'me.rayzr522.jsonmessage', 'io.illyria.shade.jsonmessage'
    relocate 'io.papermc.lib', 'io.illyria.shade.paperlib'
    archiveName 'FactionsX.jar'
}

task ci {
    dependsOn clean
    dependsOn shadowJar
}
